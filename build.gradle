/*
 * Copyright 2013 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */



apply plugin: 'idea'
apply plugin: 'groovy'
apply from: 'gradle/idea.gradle'

repositories {
    mavenCentral()
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
    golo
}

sourceSets {
    main.compileClasspath += configurations.golo
    integrationTest {
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
}

dependencies {
    compile gradleApi(), localGroovy(), 'commons-io:commons-io:1.4'
    testCompile 'org.spockframework:spock-core:0.7-groovy-1.8'
    golo 'org.golo-lang:golo:0-preview2-SNAPSHOT'
}

task archiveReports(type: Zip) {
    from reporting.baseDir
    archiveName 'reports.zip'
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

check.dependsOn integrationTest
